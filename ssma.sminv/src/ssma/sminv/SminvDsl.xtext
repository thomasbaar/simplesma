grammar ssma.sminv.SminvDsl with ssma.fml.FmlDsl

generate sminvDsl "http://www.sminv.ssma/SminvDsl"

//import "http://www.fml.ssma/FmlDsl"


// NOTE: 
// - add an Eclipse-dependency to ssma.fml in order to use FmlDsl as supergrammar
// - add MF-dep from ..ui to ssma.fml.ui (make sure everything is exported there ?!)
// - add MF-dep from ..ide to ssma.fml
// - add MF-dep from ..tests to ssma.fml
// - add MF-dep from ..tests to ssma.fml.tests
// - add MF-dep from ..tests to de.htwberlin...princessintegration
// - add MF-dep from . to de.htwberlin...princessintegration
// - adapt .mwe2 file (workflow->component->language->referencedResource)
// - inject an own TermTypeProvider in ...RuntimeModule.java
// - activate NamesAreUniqueValdator in .mwe2-file


SminvModel:
	('project' name=ID)?  // serves as namespace; avoid cross-refs to Types from other files
	vd=VarDecl
	sd=StateDecl
	ed=EventDecl
	td=TransDecl
	(id=InvDecl)?;

// all variables are of type int
// all variables are globally visible
// variables are internally initialized with 0; other initializations can be done via
// action from start-state to its successor-state

VarDecl:
	{VarDecl}
	'vars'  vars+=Var*;

	// first state is considered to be start state		
StateDecl:
	'states' states+=State+;

EventDecl:
	{EventDecl}
	'events' events+=Event*;

TransDecl:
	{TransDecl}
	'transitions' trans+=Transition*;

Var:
	name=ID;

State:
	name=ID;

Event:
	name=ID;

Transition:
	pre=[State] '=>' post=[State]
	(ev=[Event])? ('[' g=Term ']')? ('/' act+=Update+)? ';' //TODO: would be better to write g=Guard, due to outlining
;

Update:
	variable=[Var] op=('=' | '+=' | '-=') value=Term;


// Overwritten imported rule
//
// We want to add 'VarRef' as a new kind to 'Atomic' terms
//
// NOTE: Xtext recreates here new classes for NegFml, Compound, etc. and does
//       not take the inherited ones!!! Even for Term is new class is created
//       but at least this is a subclass of the inherited Term.
// TODO: find out how to avoid the new creation, since it
//       forces code-duplication in the TermTypeProvider and TermEvalProvider
Atomic returns Term:
	{NegFml} '!' t=Atomic   // NOTE: here we need Atomic instead of Term !!!
	| {Compound} '(' t=Term ')'  // 
	| {BoolConstant} value=('true'|'false') 
	| {IntConstant} value=INT
	| {VarRef} v=[Var]
;


// support for invariants
InvDecl:
	{InvDecl}
	'invariants' invs+=Inv*;
	
	
Inv:
	state=[State] ':' inv=Term ';'; 


